# Maintainer: jsteel <mail at jsteel dot org>
# Contributor: nblock <nblock [/at\] archlinux DOT us>
# Contributor: William DÃ­az <wdiaz [at] archlinux [dot] us>
# Contributor: Wolfgang Frisch <xororand@frexx.de>
# Contributor: Leandro Inacio <carvalho.inacio@gmail.com>

# This package is based on gentoo's app-vim/colorschemes.
#
# The source tarball is generated by upstream using the following script:
#   https://github.com/radhermit/gentoo-vim-scripts/blob/master/get-colorschemes

pkgname=vim-colorschemes
pkgver=20130124
pkgrel=1
pkgdesc="A very large collection of color schemes from vim.org"
url="http://packages.gentoo.org/package/app-vim/colorschemes"
arch=('any')
conflicts=('vim-colorsamplerpack' 'vim-solarized-git')
depends=('vim')
license=('vim' 'GPL2' 'public-domain' 'as-is')
groups=('vim-plugins')
source=("http://dev.gentoo.org/~radhermit/vim/colorschemes-$pkgver.tar.bz2")
md5sums=('9336cbb95edfe64b794533edb5481969')

build() {
  cd "$srcdir"/colorschemes-$pkgver

  patch -Np0 -i patches/kib_plastic.patch
  patch -Np0 -i patches/potts.patch

  # fix line endings
  find . -name '*.vim' -exec sed -i -e 's,\r,\n,g' {} \;
}

package() {
  cd "$srcdir"/colorschemes-$pkgver

  install -dm755 "$pkgdir"/usr/share/vim/vimfiles/colors
  install -m644 colors/* "$pkgdir"/usr/share/vim/vimfiles/colors/

  # delete existing colors
  rm -r "$pkgdir"/usr/share/vim/vimfiles/colors/{blue.vim,delek.vim,desert.vim,torte.vim}
}
